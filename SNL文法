1)Program::=ProgramHead DeclarePart ProgramBody .
2)ProgramHead::=PROGRAM ProgramName
3)ProgramName::=ID
4)DeclarePart::=TypeDec VarDec ProcDec
5)TypeDec::=ε
6) | TypeDeclaration
7)TypeDeclaration::=TYPE TypeDecList
8)TypeDecList::=TypeId = TypeName ; TypeDecMore
9)TypeDecMore::=ε
10) | TypeDecList
11)TypeId::=ID
12)TypeName::=BaseType
13) | StructureType
14) | ID
15)BaseType::=INTEGER
16) | CHAR
17)StructureType::=ArrayType
18) | RecType
19)ArrayType::=ARRAY [ Low .. Top ] OF BaseType
20)Low::=INTC
21)Top::=INTC
22)RecType::=RECORD FieldDecList END
23)FieldDecList::=BaseType IdList ; FieldDecMore
24) | ArrayType IdList ; FieldDecMore
25)FieldDecMore::=ε
26) | FieldDecList
27)IdList::=ID IdMore
28)IdMore::=ε
29) | , IdList
30)VarDec::=ε
31) | VarDeclaration
32)VarDeclaration::=VAR VarDecList
33)VarDecList::=TypeName VarIdList ; VarDecMore
34)VarDecMore::=ε
35) | VarDecList
36)VarIdList::=ID VarIdMore
37)VarIdMore::=ε
38) | , VarIdList
39)ProcDec::=ε
40) | ProcDeclaration
41)ProcDeclaration::=PROCEDURE ProcName ( ParamList ) ; ProcDecPart ProcBody ProcDecMore
42)ProcDecMore::=ε
43) | ProcDeclaration
44)ProcName::=ID
45)ParamList::=ε
46) | ParamDecList
47)ParamDecList::=Param ParamMore
48)ParamMore::=ε
49) | ; ParamDecList
50)Param::=TypeName FormList
51) | VAR TypeName FormList
52)FormList::=ID FidMore
53)FidMore::=ε
54) | , FormList
55)ProcDecPart::=DeclarePart
56)ProcBody::=ProgramBody
57)ProgramBody::=BEGIN StmList END
58)StmList::=Stm StmMore
59)StmMore::=ε
60) | ; StmList
61)Stm::=ConditionalStm
62) | LoopStm
63) | InputStm
64) | OutputStm
65) | ReturnStm
66) | ID AssCall
67)AssCall::=AssignmentRest
68) | CallStmRest
69)AssignmentRest::=VariMore := Exp
70)ConditionalStm::=IF RelExp THEN StmList ELSE StmList FI
71)LoopStm::=WHILE RelExp DO StmList ENDWH
72)InputStm::=READ ( Invar )
73)Invar::=ID
74)OutputStm::=WRITE ( Exp )
75)ReturnStm::=RETURN ( Exp )
76)CallStmRest::=( ActParamList )
77)ActParamList::=ε
78) | Exp ActParamMore
79)ActParamMore::=ε
80) | , ActParamList
81)RelExp::=Exp OtherRelE
82)OtherRelE::=CmpOp Exp
83)Exp::=Term OtherTerm
84)OtherTerm::=ε
85) | AddOp Exp
86)Term::=Factor OtherFactor
87)OtherFactor::=ε
88) | MultOp Term
89)Factor::=( Exp )
90) | INTC
91) | Variable
92)Variable::=ID VariMore
93)VariMore::=ε
94) | [ Exp ]
95) | . FieldVar
96)FieldVar::=ID FieldVarMore
97)FieldVarMore::=ε
98) | [ Exp ]
99)CmpOp::=<
100) | =
101)AddOp::=+
102) | -
103)MultOp::=*
104) | /